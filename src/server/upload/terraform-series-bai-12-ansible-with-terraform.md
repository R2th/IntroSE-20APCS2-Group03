## Gi·ªõi thi·ªáu
Ch√†o c√°c b·∫°n t·ªõi v·ªõi series v·ªÅ Terraform, ·ªü b√†i tr∆∞·ªõc ch√∫ng ta ƒë√£ t√¨m hi·ªÉu v·ªÅ [A/B Testing Deployment](https://viblo.asia/p/terraform-series-bai-11-terraform-ab-testing-deployment-1Je5EAWj5nL). ·ªû b√†i n√†y ch√∫ng ta s·∫Ω t√¨m hi·ªÉu v·ªÅ m·ªôt ch·ªß ƒë·ªÅu kh√° th√∫ v·ªã l√† s·ª≠ d·ª•ng k·∫øt h·ª£p Terraform v·ªõi Ansible.

![image.png](https://images.viblo.asia/23cda9b8-f419-4a5f-b754-6de4cc53f4fe.png)

## Problem
Tr∆∞·ªõc khi t√¨m hi·ªÉu v·ªÅ c√°ch d√πng Ansible trong Terraform, ta s·∫Ω n√≥i qua m·ªôt v·∫•n ƒë·ªÅ m√† ta s·∫Ω g·∫∑p khi ta x√†i Terraform, ƒë√≥ l√† sau khi ta t·∫°o h·∫° t·∫ßng b·∫±ng Terraform xong r·ªìi th√¨ l√†m sao ta c√≥ th·ªÉ config cho n√≥? V√≠ d·ª• l√† khi b·∫°n d√πng Terraform ƒë·ªÉ t·∫°o EC2 tr√™n AWS xong, ta mu·ªën c√†i nh·ªØng th·ª© m√† ta hay x√†i l√† nano, net-tools, docker th√¨ ta l√†m th·∫ø n√†o?

ƒê·ªëi v·ªõi EC2 th√¨ ta c√≥ th·ªÉ x√†i user_data, nh∆∞ng n·∫øu ta x√†i user_data th√¨ cho d√π ƒëo·∫°n script trong user_data ƒë√≥ c√≥ ch·∫°y th√†nh c√¥ng hay th·∫•t b·∫°i th√¨ Terraform c≈©ng b√°o l·∫°i cho ta l√† resource EC2 ƒë√£ t·∫°o th√†nh c√¥ng. Nh∆∞ng th·ª© ta mu·ªën l·∫°i l√† ta t·∫°o EC2 xong, ta ch·∫Øc ch·∫Øn r·∫±ng t·∫•t c·∫£ ƒëo·∫°n script m√† ta c·∫ßn ch·∫°y tr√™n con EC2 ƒë√≥ th√†nh c√¥ng h·∫øt th√¨ Terraform m·ªõi b√°o l·∫°i cho ta l√† ƒë√£ t·∫°o resource th√†nh c√¥ng.

ƒê·ªÉ gi·∫£i quy·∫øt v·∫•n ƒë·ªÅ ƒë√≥ th√¨ Terraform sinh ra t√≠nh nƒÉng t√™n l√† **Provisioners**.

## Provisioners
Provisioners l√† m·ªôt t√≠nh nƒÉng cho ph√©p ta th·ª±c thi ƒëo·∫°n script ·ªü d∆∞·ªõi m√°y local, ho·∫∑c ch·∫°y ƒëo·∫°n script ·ªü tr√™n remote resource. Th∆∞·ªùng ƒë∆∞·ª£c s·ª≠ d·ª•ng ƒë·ªÉ c·∫•u h√¨nh h·∫° t·∫ßng sau khi h·∫° t·∫ßng ƒë∆∞·ª£c t·∫°o ra. Provisioners c√≥ hai lo·∫°i l√†:
+ local-exec: d√πng ƒë·ªÉ ch·∫°y script ·ªü d∆∞·ªõi m√°y local m√† Terraform ƒëang ch·∫°y, **ta s·∫Ω d√πng n√†y ƒë·ªÉ ch·∫°y Ansible**.
+ remote-exec: d√πng ƒë·ªÉ ch·∫°y script ·ªü m√°y remote. V√≠ d·ª• ta t·∫°o EC2 xong th√¨ ta s·∫Ω d√πng remote-exec ƒë·ªÉ ch·∫°y ƒëo·∫°n script ·ªü tr√™n con EC2 m·ªõi t·∫°o.

V√≠ d·ª• ta s·∫Ω d√πng remote-exec ƒë·ªÉ c√†i Apache HTTP Server l√™n con EC2, t·∫°o m·ªôt file t√™n l√† `main.tf`.

```main.tf
provider "aws" {
  region = "us-west-2"
}
```

Ti·∫øp theo, ta t·∫°o SSH key pair cho con EC2 ta c·∫ßn t·∫°o.

```main.tf
provider "aws" {
  region = "us-west-2"
}

resource "tls_private_key" "key" {
  algorithm = "RSA"
}

resource "aws_key_pair" "key_pair" {
  key_name   = "ansible-key"
  public_key = tls_private_key.key.public_key_openssh
}

```

C·∫•u h√¨nh Security Group cho ph√©p ssh v√†o EC2.

```main.tf
...
resource "aws_security_group" "allow_ssh" {
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}
```

T·∫°o EC2.

```main.tf
...
data "aws_ami" "ami" {
  most_recent = true

  filter {
    name   = "name"
    values = ["amzn2-ami-hvm-2.0.*-x86_64-gp2"]
  }

  owners = ["amazon"]
}

resource "aws_instance" "ansible_server" {
  ami                    = data.aws_ami.ami.id
  instance_type          = "t3.micro"
  vpc_security_group_ids = [aws_security_group.allow_ssh.id]
  key_name               = aws_key_pair.key_pair.key_name

  tags = {
    Name = "Apache Server"
  }
}
```

Oke, gi·ªù ta s·∫Ω d√πng remote-exec nh∆∞ sau.

```main.tf
...
resource "aws_instance" "ansible_server" {
  ami                    = data.aws_ami.ami.id
  instance_type          = "t3.micro"
  vpc_security_group_ids = [aws_security_group.allow_ssh.id]
  key_name               = aws_key_pair.key_pair.key_name

  provisioner "remote-exec" {
    inline = [
      "sudo yum update -y",
      "sudo yum install -y httpd.x86_64",
      "sudo systemctl start httpd",
      "sudo systemctl enable httpd"
    ]

    connection {
      type        = "ssh"
      user        = "ec2-user"
      private_key = tls_private_key.key.private_key_pem
      host        = self.public_ip
    }
  }

  tags = {
    Name = "Apache Server"
  }
}
```

·ªû tr√™n ta x√†i provisioner v·ªõi lo·∫°i remote-exec, ƒë·ªÉ provisioner c√≥ th·ªÉ k·∫øt n·ªëi ƒë∆∞·ª£c v·ªõi m√°y remote, ta c·∫ßn ph·∫£i c·∫•u h√¨nh authentication cho n√≥ ·ªü block `connection`.

```
provisioner "remote-exec" {
    ...

    connection {
      type        = "ssh"
      user        = "ec2-user"
      private_key = tls_private_key.key.private_key_pem
      host        = self.public_ip
    }
}
```

Block inline s·∫Ω ch·ª©a nh·ªØng c√¢u l·ªánh ta c·∫ßn th·ª±c thi ·ªü m√°y remote, ·ªü tr√™n l√† nh·ªØng c√¢u cli d√πng ƒë·ªÉ c√†i apache server.

```
provisioner "remote-exec" {
    inline = [
      "sudo yum update -y",
      "sudo yum install -y httpd.x86_64",
      "sudo systemctl start httpd",
      "sudo systemctl enable httpd"
    ]

    ...
}
```

Gi·ªù ta ch·∫°y c√¢u l·ªánh init v√† apply n√†o.

```
$ terraform init && terraform apply
```

L√∫c n√†y b·∫°n s·∫Ω th·∫•y sau khi EC2 ƒë∆∞·ª£c t·∫°o ra, terraform s·∫Ω k·∫øt n·ªëi t·ªõi n√≥ v√† ch·∫°y c√°c c√¢u CLI, sau ƒë√≥ n·∫øu c√°c c√¢u l·ªánh ch·∫°y xong h·∫øt th√¨ terraform m·ªõi b√°o l·∫°i l√† EC2 ƒë√£ ƒë∆∞·ª£c t·∫°o th√†nh c√¥ng.

```bash
...
aws_instance.ansible_server: Provisioning with 'remote-exec'...
aws_instance.ansible_server (remote-exec): Connecting to remote host via SSH...
aws_instance.ansible_server (remote-exec):   Host: 35.86.209.174
aws_instance.ansible_server (remote-exec):   User: ec2-user
aws_instance.ansible_server (remote-exec):   Password: false
aws_instance.ansible_server (remote-exec):   Private key: true
aws_instance.ansible_server (remote-exec):   Certificate: false
aws_instance.ansible_server (remote-exec):   SSH Agent: false
aws_instance.ansible_server (remote-exec):   Checking Host Key: false
aws_instance.ansible_server (remote-exec):   Target Platform: unix
aws_instance.ansible_server (remote-exec): Connected!
...
Apply complete! Resources: 2 added, 0 changed, 0 destroyed.

Outputs:

ec2 = "44.235.74.32"
```

Oke, ta ƒë√£ s·ª≠ d·ª•ng provisioner ƒë·ªÉ c·∫•u h√¨nh cho EC2 th√†nh c√¥ng üòÅ. N·∫øu ta EC2 c·ªßa ta ch·ªâ c·∫ßn c·∫•u h√¨nh ƒë∆°n gi·∫£n th√¨ ta ch·ªâ c·∫ßn d√πng remote-exec ƒë·ªÉ ch·∫°y m·∫•y c√¢u CLI ƒë∆°n gi·∫£n nh∆∞ v·∫≠y l√† ƒë∆∞·ª£c, nh∆∞ng n·∫øu EC2 c·ªßa ta c·∫ßn c·∫•u h√¨nh ph·ª©c t·∫°p h∆°n nhi·ªÅu th√¨ ta kh√¥ng th·ªÉ ch·ªâ s·ª≠ d·ª•ng m·∫•y c√¢u CLI ƒë∆∞·ª£c, m√† ta c·∫ßn d√πng c√¥ng c·ª• g·ªçi l√† **Configuration Management**.

## Ansible
Khi ta d√πng Terraform, ta ch·ªâ s·ª≠ d·ª•ng n√≥ cho vi·ªác provisioning h·∫° t·∫ßng, c√≤n vi·ªác c·∫•u h√¨nh h·∫° t·∫ßng th√¨ ta kh√¥ng n√™n d√πng Terraform v√¨ ƒë√≥ kh√¥ng ph·∫£i lƒ©nh v·ª±c c·ªßa n√≥, m√† ta n√™n d√πng configuration management. Trong nh·ªØng c√¥ng c·ª• configuration management th√¨ c√≥ l·∫Ω Ansible l√† ƒë∆∞·ª£c s·ª≠ d·ª•ng r·ªông r√£i nh·∫•t. M√¥ h√¨nh ph·ªï bi·∫øn nh∆∞ sau.

![](https://images.viblo.asia/7a96b89f-92d4-45d6-af49-e3965c0458b3.png)

ƒê·ªÉ s·ª≠ d·ª•ng Ansible trong Terraform, ƒë·∫ßu ti√™n ta ph·∫£i d√πng remote-exec ƒë·ªÉ c√†i Ansible l√™n tr√™n remote server, sau ƒë√≥ ta s·∫Ω d√πng local-exec ƒë·ªÉ th·ª±c thi Ansible playbook ·ªü d∆∞·ªõi m√°y local.

V√≠ d·ª• ta t·∫°o EC2 v√† d√πng Ansible ƒë·ªÉ c√†i Nginx l√™n tr√™n n√≥, t·∫°o hai file t√™n l√† `main.tf` v√† `playbook.yaml`.

```main.tf
provider "aws" {
  region  = "us-west-2"
}

resource "tls_private_key" "key" {
  algorithm = "RSA"
}

resource "local_sensitive_file" "private_key" {
  filename        = "${path.module}/ansible.pem"
  content         = tls_private_key.key.private_key_pem
  file_permission = "0400"
}

resource "aws_key_pair" "key_pair" {
  key_name   = "ansible"
  public_key = tls_private_key.key.public_key_openssh
}

resource "aws_security_group" "allow_ssh" {
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

data "aws_ami" "ubuntu" {
  most_recent = true

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }

  owners = ["099720109477"]
}

resource "aws_instance" "ansible_server" {
  ami                    = data.aws_ami.ubuntu.id
  instance_type          = "t3.micro"
  vpc_security_group_ids = [aws_security_group.allow_ssh.id]
  key_name               = aws_key_pair.key_pair.key_name

  provisioner "remote-exec" {
    inline = [
      "sudo apt update -y",
      "sudo apt install -y software-properties-common",
      "sudo apt-add-repository --yes --update ppa:ansible/ansible",
      "sudo apt install -y ansible"
    ]

    connection {
      type        = "ssh"
      user        = "ubuntu"
      private_key = tls_private_key.key.private_key_pem
      host        = self.public_ip
    }
  }

  tags = {
    Name = "Ansible Server"
  }
}

output "ec2" {
  value = aws_instance.ansible_server.public_ip
}
```

Code ·ªü tr√™n c≈©ng t∆∞∆°ng t·ª± khi n√£y, ch·ªâ kh√°c ·ªü m·ªôt ch·ªó l√† ta th√™m v√†o m·ªôt resource l√† `local_sensitive_file`, d√πng ƒë·ªÉ output ra file pem m√† ta s·∫Ω d√πng n√≥ cho Ansible.

```main.tf
...
resource "local_sensitive_file" "private_key" {
  filename        = "${path.module}/ansible.pem"
  content         = tls_private_key.key.private_key_pem
  file_permission = "0400"
}
...
```

Sau ƒë√≥ ƒë·ªÉ x√†i ansible, ta d√πng local-exec nh∆∞ sau.

```main.tf
...
resource "aws_instance" "ansible_server" {
  ami                    = data.aws_ami.ubuntu.id
  instance_type          = "t3.micro"
  vpc_security_group_ids = [aws_security_group.allow_ssh.id]
  key_name               = aws_key_pair.key_pair.key_name

  provisioner "remote-exec" {
    inline = [
      "sudo apt update -y",
      "sudo apt install -y software-properties-common",
      "sudo apt-add-repository --yes --update ppa:ansible/ansible",
      "sudo apt install -y ansible"
    ]

    connection {
      type        = "ssh"
      user        = "ubuntu"
      private_key = tls_private_key.key.private_key_pem
      host        = self.public_ip
    }
  }

  provisioner "local-exec" {
    command = "ANSIBLE_HOST_KEY_CHECKING=False ansible-playbook -u ubuntu --key-file ansible.pem -T 300 -i '${self.public_ip},', playbook.yaml"
  }

  tags = {
    Name = "Ansible Server"
  }
}
```

ƒê√¢y l√† ƒëo·∫°n ta s·∫Ω th·ª±c thi Ansible.

```
provisioner "local-exec" {
    command = "ANSIBLE_HOST_KEY_CHECKING=False ansible-playbook -u ubuntu --key-file ansible.pem -T 300 -i '${self.public_ip},', playbook.yaml"
}
```

**Nh·ªõ l√† d∆∞·ªõi m√°y ta c·∫ßn c√†i Ansible tr∆∞·ªõc nh√©**. Sau ƒë√≥ ta c·∫≠p nh·∫≠t file playbook.yaml l√† ch·ª©a code c·ªßa Ansible.

```playbook.yaml
- name: Install Nginx
  hosts: all
  become: true
  tasks:
    - name: Install Nginx
      apt:
        name: nginx
        state: present
    - name: Add index page
      template:
        src: index.html
        dest: /var/www/html/index.html
    - name: Start Nginx
      service:
        name: nginx
        state: started
```

**N·∫øu b·∫°n ch·∫°y ansible tr√™n centos th√¨ s·ª≠a ch·ªó apt th√†nh yum nh√©**. Ti·∫øp theo ta t·∫°o m·ªôt file `index.html` ƒë·ªÉ Ansible copy n√≥ l√™n tr√™n server.

```index.html
<!DOCTYPE html>
<html>
  <style>
    body {
      background-color: green;
      color: white;
    }
  </style>
  <body>
    <h1>Ansible</h1>
  </body>
</html>
```

Gi·ªù ta ch·∫°y c√¢u l·ªánh init v√† apply n√†o.

```
terraform init && terraform apply
```

B·∫°n s·∫Ω th·∫•y local-exec s·∫Ω th·ª±c thi ansbile.

```bash
...
aws_instance.ansible_server: Provisioning with 'local-exec'...
aws_instance.ansible_server (local-exec): Executing: ["/bin/sh" "-c" "ANSIBLE_HOST_KEY_CHECKING=False ansible-playbook -u ubuntu --key-file ansible.pem -T 300 -i '35.87.91.3,', playbook.yaml"]

aws_instance.ansible_server (local-exec): PLAY [Install Nginx] ***********************************************************

aws_instance.ansible_server (local-exec): TASK [Gathering Facts] *********************************************************
aws_instance.ansible_server: Still creating... [1m30s elapsed]
aws_instance.ansible_server (local-exec): ok: [35.87.91.3]

aws_instance.ansible_server (local-exec): TASK [Install Nginx] ***********************************************************
aws_instance.ansible_server: Still creating... [1m40s elapsed]
aws_instance.ansible_server: Still creating... [1m50s elapsed]
aws_instance.ansible_server (local-exec): changed: [35.87.91.3]

aws_instance.ansible_server (local-exec): TASK [Add index page] **********************************************************
aws_instance.ansible_server: Still creating... [2m0s elapsed]
aws_instance.ansible_server (local-exec): changed: [35.87.91.3]

aws_instance.ansible_server (local-exec): TASK [Start Nginx] *************************************************************
aws_instance.ansible_server (local-exec): ok: [35.87.91.3]

aws_instance.ansible_server (local-exec): PLAY RECAP *********************************************************************
aws_instance.ansible_server (local-exec): 35.87.91.3                 : ok=4    changed=2    unreachable=0    failed=0    skipped=0    rescued=0    ignored=0

aws_instance.ansible_server: Creation complete after 2m7s [id=i-0fd0e63361c597de1]

Apply complete! Resources: 2 added, 0 changed, 0 destroyed.

Outputs:

ec2 = "35.87.91.3"
```

Sau khi n√≥ ch·∫°y xong th√¨ truy c·∫≠p v√†o IP c·ªßa EC2 ta v·ª´a t·∫°o, b·∫°n s·∫Ω th·∫•y nginx server ƒëang host file index.html c·ªßa ta.

![image.png](https://images.viblo.asia/f9dad437-bfe3-4a7a-b147-bece8aa35ace.png)

Oke, v·∫≠y l√† ta ƒë√£ k·∫øt h·ª£p ƒë∆∞·ª£c Terraform v√† Ansible üòÅ.

Github c·ªßa to√†n b·ªô series https://github.com/hoalongnatsu/terraform-series.

## Deep into Provisioners
Ta s·∫Ω n√≥i r√µ h∆°n v·ªÅ provisioners m·ªôt ch√∫t, do b√†i n√†y ti√™u ƒë·ªÅ l√† v·ªÅ Terraform v·ªõi Ansible m√† n√≥i l√Ω thuy·∫øt nhi·ªÅu qu√° ch·∫Øc c√°c b·∫°n s·∫Ω ch√°n, n√™n m√¨nh l√†m v√≠ d·ª• ph·∫ßn ch√≠nh tr∆∞·ªõc r·ªìi s·∫Ω n√≥i kƒ© ph·∫ßn l√Ω thuy·∫øt sau, ai c·∫ßn s·∫Ω ƒë·ªçc ƒë·ªÉ ƒëi ph·ªèng v·∫•n th√¥i `:))))`.

### Creation-Time and Destruction-time

·ªû tr√™n ta ƒë√£ d√πng provisioner ƒë·ªÉ th·ª±c thi c√°c c√¢u CLI, v√† h·∫ßu h·∫øt n√≥ ƒë·ªÅu ch·∫°y ·ªü l√∫c terraform t·∫°o resource, v√† provisioner c≈©ng c√≥ th·ªÉ c·∫•u h√¨nh ƒë·ªÉ ch·∫°y l√∫c terraform destroy resource ƒë·ªÉ ta clear up server. Provisioner s·∫Ω ƒë∆∞·ª£c th·ª±c thi ·ªü hai lifecycle sau:
+ Creation-time provisioners.
+ Destruction-time provisioners.

V·ªõi creation-time provisioners th·ª±c thi l√∫c resource ƒë∆∞·ª£c t·∫°o ra v√† destruction-time provisioners th·ª±c thi l√∫c resouce ƒë∆∞·ª£c x√≥a ƒëi.

V√≠ d·ª•:

```
resource "google_project_service" "enabled_service" {
  for_each = toset(local.services)
  project  = var.project_id
  service  = each.key

  provisioner "local-exec" {
    command = "sleep 60"
  }

  provisioner "local-exec" {
    when    = destroy
    command = "sleep 15"
  }
}
```

Creation-time provisioners.

![image.png](https://images.viblo.asia/cf6c5b49-7795-4e26-8591-74cdb680d982.png)

Destruction-time provisioners.

![image.png](https://images.viblo.asia/ab9d5a21-aa9f-4388-9db0-06e7db0462e9.png)

V·ªõi destruction-time provisioners th√¨ ta s·∫Ω th√™m tr∆∞·ªùng `when = destroy` v√†o, l√∫c n√†y terraform s·∫Ω hi·ªÉu ta c·∫ßn ch·∫°y provisioner n√†y ·ªü l√∫c destroy resource.

### Failure Behavior
N·∫øu provisioner c·ªßa ta ch·∫°y th·∫•t b·∫°i th√¨ sao? M·∫∑c ƒë·ªãnh n·∫øu ta d√πng provisioner th√¨ khi n√≥ ch·∫°y th·∫•t b·∫°i, resource c·ªßa ta s·∫Ω b·ªã ƒë√°nh l√† fail lu√¥n. Ta c√≥ th·ªÉ c·∫•u h√¨nh n·∫øu provisioner th·∫•t b·∫°i th√¨ terraform s·∫Ω xem resource ƒë√≥ l√† th·∫•t b·∫°i lu√¥n, hay l√† resource ƒë√≥ v·∫´n ƒë∆∞·ª£c terraform ƒë√°nh l√† t·∫°o ra th√†nh c√¥ng v√† ch·∫°y ti·∫øp.

Ta s·∫Ω d√πng thu·ªôc t√≠nh `on_failure` c·ªßa provisioner ƒë·ªÉ c·∫•u h√¨nh vi·ªác n√†y, n√≥ c√≥ hai gi√° tr·ªã l√† `continue` v√† `fail`, m·∫∑c ƒë·ªãnh l√† fail. V√≠ d·ª•, ta c·∫•u h√¨nh nh∆∞ sau ƒë·ªÉ khi provisioner c√≥ ch·∫°y th·∫•t b·∫°i th√¨ terraform v·∫´n coi n√≥ l√† th√†nh c√¥ng.

```
resource "aws_instance" "web" {
  ...

  provisioner "local-exec" {
    command    = "echo The server's IP address is ${self.private_ip}"
    on_failure = continue
  }
}
```

## K·∫øt lu·∫≠n
V·∫≠y l√† ta ƒë√£ t√¨m hi·ªÉu xong v·ªÅ provisioner v√† c√°ch d√πng n√≥ ƒë·∫ª k·∫øt h·ª£p Terraform v·ªõi Ansible üòÅ. N·∫øu c√≥ th·∫Øc m·∫Øc ho·∫∑c c·∫ßn gi·∫£i th√≠ch r√µ th√™m ch·ªó n√†o th√¨ c√°c b·∫°n c√≥ th·ªÉ h·ªèi d∆∞·ªõi ph·∫ßn comment.

## M·ª•c t√¨m ki·∫øm ƒë·ªìng ƒë·ªôi

![Ho√†ng Ph√∫c](https://images.viblo.asia/9fbde6d9-5e57-4429-a903-10a961e1c96c.png)

Team c√¥ng ngh·ªá Ho√†ng Ph√∫c c·ªßa b·ªçn m√¨nh ƒë∆∞·ª£c th√†nh l·∫≠p v·ªõi nhi·ªám v·ª• l√† x√¢y d·ª±ng m·ªôt h·ªá th·ªëng c√¥ng ngh·ªá n·ªôi b·ªô cho c√¥ng ty, Ho√†ng Ph√∫c l√† m·ªôt c√¥ng ty b√°n l·∫ª trong lƒ©nh v·ª±c th·ªùi trang v√† c√≥ h∆°n 30 nƒÉm tu·ªïi ƒë·ªùi, v·ªõi chu·ªói c·ª≠a h√†ng r·∫•t nhi·ªÅu tr√™n to√†n qu·ªëc, n√™n vi·ªác v·∫≠n h√†nh c·ªßa Ho√†ng Ph√∫c l√† r·∫•t l·ªõn v√† vi·ªác x√¢y d·ª±ng ƒë∆∞·ª£c m·ªôt h·ªá th·ªëng c√¥ng ngh·ªá ƒë·ªÉ ƒë√°p ·ª©ng vi·ªác v·∫≠n h√†nh n·ªôi b·ªô cho c√¥ng ty l√† m·ªôt c√¥ng vi·ªác r·∫•t th·ª≠ th√°ch, ƒë√¢y l√† m·ªôt qu√° tr√¨nh chuy·ªÉn ƒë·ªïi s·ªë v√† team b·ªçn m√¨nh ƒë√£ l√†m ƒë∆∞·ª£c nh·ªØng b∆∞·ªõc ban ƒë·∫ßu.

Th·ª© m√† team m√¨nh th·∫•y c·∫•n duy nh·∫•t l√† c√°i website, ƒë√¢y l√† trang web m√† tr∆∞·ªõc khi team m√¨nh ƒë∆∞·ª£c th√†nh l·∫≠p ƒë√£ c√≥ m·ªôt ƒë·ªôi outsource kh√°c l√†m, v√† nh·ªØng g√¨ h·ªç ƒë·ªÉ l·∫°i cho b·ªçn m√¨nh l√† m·ªôt trang web v·ªõi ƒë·ªëng b√πi nh√πi, v·ªõi s·ªë ƒëi·ªÉm t·ª´ google l√† 1 tr√™n 100. V·∫≠y b·ªçn m√¨nh s·∫Ω l√†m g√¨ v·ªõi trang web n√†y ƒë√¢y, n·∫£n ch√≠ sao? ƒêi·ªÅu ƒë√≥ kh√¥ng c√≥ trong t·ª´ ƒëi·ªÉn c·ªßa hai s·∫øp m√¨nh, v√† v·ªõi s·ª± d·∫´n d·∫Øt c·ªßa hai s·∫øp team m√¨nh s·∫Ω bi·∫øn ƒë·ªëng website b√πi nh√πi ƒë√≥ th√†nh kim c∆∞∆°ng, nh∆∞ c√°ch b·ªçn m√¨nh ƒë√£ c·∫£i thi·ªán h·ªá th·ªëng n·ªôi b·ªô. B·ªçn m√¨nh ƒëang c·∫£i thi·ªán trang web h·∫±ng ng√†y v√† h·∫±ng ng√†y, t·ª´ 1 ƒëi·ªÉm b·ªçn m√¨nh ƒë√£ c·∫£i thi·ªán n√≥ l√™n 70 ƒëi·ªÉm, v√† m·ª•c ti√™u l√† tr√™n 90 ƒëi·ªÉm.

Hi·ªán t·∫°i team b·ªçn m√¨nh ƒëang c·∫ßn c√°c ƒë·ªìng ƒë·ªôi tham gia ƒë·ªÉ c·∫£i thi·ªán l·∫°i trang web v·ªõi s·ªë l∆∞·ª£ng ng∆∞·ªùi d√πng truy c·∫≠p kh√° l·ªõn, ƒë√¢y l√† m·ªôt th·ª≠ th√°ch r·∫•t th√∫ v·ªã, c√≥ bao gi·ªù c√°c b·∫°n ƒë∆∞·ª£c tham gia thi·∫øt k·∫ø m·ªôt h·ªá th·ªëng l·ªõn t·ª´ ƒë·∫ßu ch∆∞a, m√¨nh kh√° ch·∫Øc l√† s·ªë l∆∞·ª£ng ƒë√≥ r·∫•t √≠t. B·ªçn m√¨nh ƒë√£ c√≥ kh√°ch h√†ng, nh·ªØng g√¨ c√≤n l·∫°i l√† c·∫ßn nh·ªØng ƒë·ªìng ƒë·ªôi ƒë·ªÉ c√πng nhau ph√°t tri·ªÉn m·ªôt h·ªá th·ªëng ƒë·ªÉ ph·ª•c v·ª• r·∫•t nhi·ªÅu ng∆∞·ªùi d√πng. M·ª•c ti√™u c·ªßa c√¥ng ty Ho√†ng Ph√∫c l√† tr·ªü th√†nh nh√† b√°n l·∫ª v·ªÅ th·ªùi trang l·ªõn nh·∫•t Vi·ªát Nam, h√£y tham gia v·ªõi b·ªçn m√¨nh nh√©. M·ªôt th√†nh vi√™n trong team m√¨nh kh√¥ng y√™u c·∫ßn ph·∫£i gi·ªèi, ch·ªâ c·∫ßn h√≤a ƒë·ªìng, h·ª£p t√°c v√† s·∫µn s√†ng h·ª£p t√°c v·ªõi nhau. C√≥ th·ªÉ b·∫°n kh√¥ng l√† gi·ªèi nh·∫•t nh∆∞ng n·∫øu gia nh·∫≠p v·ªõi b·ªçn m√¨nh th√¨ b·∫°n s·∫Ω t·∫°o ra ƒë∆∞·ª£c nh·ªØng th·ª© gi√° tr·ªã nh·∫•t.

ƒê·ªìng ƒë·ªôi [Backend Engineer (Magento - PHP)](https://tuyendung.hoang-phuc.com/job/backend-engineer-magento-php-1538).

ƒê·ªìng ƒë·ªôi [Senior Backend Engineer](https://tuyendung.hoang-phuc.com/job/senior-backend-engineer-1022).

ƒê·ªìng ƒë·ªôi [Senior Frontend Engineer](https://tuyendung.hoang-phuc.com/job/senior-frontend-engineer-1021).