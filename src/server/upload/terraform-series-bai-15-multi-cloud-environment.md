## Gi·ªõi thi·ªáu
Ch√†o c√°c b·∫°n t·ªõi v·ªõi series v·ªÅ Terraform, ·ªü b√†i tr∆∞·ªõc ch√∫ng ta ƒë√£ t√¨m hi·ªÉu v·ªÅ c√°ch s·ª≠ d·ª•ng Terraform v·ªõi Jenkins. ·ªû b√†i ti·∫øp theo n√†y ch√∫ng ta s·∫Ω t√¨m hi·ªÉu v·ªÅ m·ªôt ch·ªß ƒë·ªÅ r·∫•t th√∫ v·ªã l√† c√°ch s·ª≠ d·ª•ng Terraform v·ªõi multi-cloud.

![image.png](https://images.viblo.asia/190c8f57-e979-44d2-9c5b-ec025536d9ef.png)

ƒêi·ªÉm m·∫°nh c·ªßa Terraform so v·ªõi c√°c c√¥ng c·ª• kh√°c l√† n√≥ gi√∫p ta d·ªÖ d√†ng t·∫°o ƒë∆∞·ª£c h·∫° t·∫ßng tr√™n nhi·ªÅu m√¥i tr∆∞·ªùng Cloud kh√°c nhau, ·ªü b√†i n√†y ch√∫ng ta s·∫Ω d√πng Terraform ƒë·ªÉ t·∫°o connection gi·ªØa hai Virtual Private Cloud c·ªßa AWS v√† GCP. C√°c b·∫°n c√≥ th·ªÉ ƒë·ªçc c√°ch c·∫•u h√¨nh b·∫±ng tay ·ªü b√†i n√†y [Connect VPC Network between AWS and GCP use VPN](https://viblo.asia/p/aws-practice-connect-vpc-network-between-aws-and-gcp-use-vpn-gDVK2rM0KLj).

## Terraform and Multi Cloud
Multi-cloud l√† c√°ch ta s·ª≠ d·ª•ng nhi·ªÅu nh√† cung c·∫•p cloud kh√°c nhau ƒë·ªÉ x√¢y d·ª±ng h·∫° t·∫ßng, v√≠ d·ª• ta s·ª≠ d·ª•ng c·∫£ AWS v√† GCP cho h·∫° t·∫ßng.

V√¨ m·ªói Cloud s·∫Ω c√≥ ƒëi·ªÉm m·∫°nh v√† ƒëi·ªÉm y·∫øu kh√°c nhau, n√™n khi ta c√≥ nhi·ªÅu l·ª±a ch·ªçn ph√π h·ª£p th√¨ h·ªá th·ªëng c·ªßa ta s·∫Ω c√†ng m·∫°nh m·∫Ω.

M·ªôt v√†i ƒëi·ªÉm m·∫°nh khi ta s·ª≠ d·ª•ng multi-cloud:
+ Flexibility: b·∫°n c√≥ th·ªÉ ch·ªçn cloud service ph√π h·ª£p nh·∫•t cho ·ª©ng d·ª•ng c·ªßa b·∫°n.
+ Cost savings: l·ª±a ch·ªçn pricing models ph√π h·ª£p nh·∫•t gi·ªØa c√°c cloud vendors.
+ Resilience: more resilient than single-cloud architectures.
+ Compliance: y·∫øu t·ªë n·ªôi b·ªô. V√≠ d·ª• n·∫øu ta ph√°t tri·ªÉn ·ª©ng d·ª•ng ·ªü Trung Qu·ªëc th√¨ ta n√™n x√†i AliCloud.

V√† v·ªõi Terraform vi·ªác t·∫°o h·∫° t·∫ßng tr√™n nhi·ªÅu m√¥i tr∆∞·ªùng Cloud kh√°c nhau kh√¥ng h·ªÅ kh√≥, Terraform c√≥ cung c·∫•p s·∫µn cho ta c√°c provider ph√π h·ª£p cho t·ª´ng m√¥i tr∆∞·ªùng.

![image.png](https://images.viblo.asia/fa8198b9-31a5-4fd4-afff-59126c68dc62.png)

## Site to Site VPN between AWS and GCP
### Architecture
H·∫° t·∫ßng m√† ta s·∫Ω x√¢y nh∆∞ sau.

![image.png](https://images.viblo.asia/190c8f57-e979-44d2-9c5b-ec025536d9ef.png)

·ªû tr√™n AWS v√† GCP ta s·∫Ω c√≥ m·ªôt VPC, sau ƒë√≥ ƒë·ªÉ k·∫øt n·ªëi gi·ªØa hai VPC th√¨ ·ªü AWS ta s·∫Ω t·∫°o Virtual Private Gateway, Site-to-Site VPN, Customer Gateway. ·ªû GCP ta s·∫Ω t·∫°o External IP, Cloud VPN. Sau ƒë√≥ ta s·∫Ω t·∫°o tunnel ƒë·ªÉ hai b√™n k·∫øt n·ªëi v·ªõi nhau.

### Config Provider
Ok, gi·ªù ta b·∫Øt tay v√†o code n√†o, ta t·∫°o ba file t√™n l√† `main.tf`, `aws.tf`, `gcp.tf`.

```main.tf
provider "aws" {
  region      = "us-west-2"
}

provider "google" {
  project     = "hpi-111111"
  region      = "us-west2"
}
```

ƒê·ªÉ s·ª≠ d·ª•ng AWS ta s·∫Ω d√πng `provider aws` (trong series n√†y ta ƒë√£ s·ª≠ d·ª•ng r·∫•t nhi·ªÅu), v√† ƒë·ªÉ s·ª≠ d·ª•ng Google Cloud ta d√πng `provider google`. C·∫•u h√¨nh c·ªßa provider google nh∆∞ sau.

```
provider "google" {
  project     = <project-id>
  region      = <region>
}
```

### Create VPC
Ti·∫øp theo ta t·∫°o GCP Virtual Private Cloud v√† AWS Virtual Private Cloud.

```gcp.tf
resource "google_compute_network" "aws_gcp" {
  name = "aws-gcp"
}
```

```aws.tf
data "aws_availability_zones" "available" {}

module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "3.14.2"

  name = "aws-gcp"
  cidr = "10.0.0.0/16"
  azs  = data.aws_availability_zones.available.names

  private_subnets = ["10.0.1.0/24", "10.0.2.0/24", "10.0.3.0/24"]
  public_subnets  = ["10.0.101.0/24", "10.0.102.0/24", "10.0.103.0/24"]

  enable_nat_gateway = true
  single_nat_gateway = true
}
```

### GCP External IP and AWS Customer Gateway
Sau ƒë√≥ ta s·∫Ω t·∫°o GCP External IP v√† AWS Customer Gateway cho hai th·∫±ng VPC.

```gcp.tf
...

resource "google_compute_address" "aws_customer_gateway" {
  name = "aws-customer-gateway"
}
```

```aws.tf
...

resource "aws_customer_gateway" "gcp_customer_gateway" {
  bgp_asn    = 65000
  ip_address = google_compute_address.aws_customer_gateway.address
  type       = "ipsec.1"

  tags = {
    Name = "gcp-customer-gateway"
  }
}
```

V·ªõi ip_address c·ªßa AWS Customer Gateway ta l·∫•y t·ª´ GCP External IP.

### VPN Gateway
Ti·∫øp ƒë√≥ ta t·∫°o VPN Gateway cho GCP VPC v√† AWS VPC.

```gcp.tf
...

resource "google_compute_vpn_gateway" "aws_gcp" {
  name    = "aws-gcp"
  network = google_compute_network.aws_gcp.id
}

resource "google_compute_forwarding_rule" "fr_esp" {
  name        = "fr-esp"
  ip_protocol = "ESP"
  ip_address  = google_compute_address.aws_customer_gateway.address
  target      = google_compute_vpn_gateway.aws_gcp.id
}

resource "google_compute_forwarding_rule" "fr_udp500" {
  name        = "fr-udp500"
  ip_protocol = "UDP"
  port_range  = "500"
  ip_address  = google_compute_address.aws_customer_gateway.address
  target      = google_compute_vpn_gateway.aws_gcp.id
}

resource "google_compute_forwarding_rule" "fr_udp4500" {
  name        = "fr-udp4500"
  ip_protocol = "UDP"
  port_range  = "4500"
  ip_address  = google_compute_address.aws_customer_gateway.address
  target      = google_compute_vpn_gateway.aws_gcp.id
}
```

Ch·ªó n√†y th√¨ ta kh√¥ng c·∫ßn hi·ªÖu kƒ© l·∫Øm, ph·∫ßn quan tr·ªçng l√† ·ªü resource `google_compute_vpn_gateway`, ta s·∫Ω ch·ªâ ƒë·ªãnh GCP VPC m√† ta mu·ªën g·∫Øn VPN Gateway n√†y v√†o b·∫±ng thu·ªôc t√≠nh `network`.

```aws.tf
...

resource "aws_vpn_gateway" "aws_gcp" {
  vpc_id = module.vpc.vpc_id

  tags = {
    Name = "AWS-GCP"
  }
}
```

T∆∞∆°ng t·ª± cho b√™n AWS.

### GCP Cloud VPN and AWS Site-to-Site VPN
Ti·∫øp theo ta s·∫Ω t·∫°o VPN ƒë·ªÉ k·∫øt n·ªëi c·∫£ hai VPC v·ªõi nhau.

```gcp.tf
...

resource "google_compute_vpn_tunnel" "tunnel_1" {
  name          = "tunnel-1"
  peer_ip       = aws_vpn_connection.aws_gcp.tunnel1_address
  shared_secret = aws_vpn_connection.aws_gcp.tunnel1_preshared_key

  target_vpn_gateway = google_compute_vpn_gateway.aws_gcp.id

  depends_on = [
    google_compute_forwarding_rule.fr_esp,
    google_compute_forwarding_rule.fr_udp500,
    google_compute_forwarding_rule.fr_udp4500,
  ]
}

resource "google_compute_route" "route_1" {
  name       = "route-1"
  network    = google_compute_network.aws_gcp.name
  dest_range = module.vpc.vpc_cidr_block
  priority   = 1000

  next_hop_vpn_tunnel = google_compute_vpn_tunnel.tunnel_1.id
}
```

·ªû b√™n GCP ta t·∫°o tunnel v·ªõi gi√° tr·ªã VPN `tunnel1_address` v√† `tunnel1_preshared_key` t·ª´ AWS.

```aws.tf
...

resource "aws_vpn_connection" "aws_gcp" {
  customer_gateway_id = aws_customer_gateway.gcp_customer_gateway.id
  vpn_gateway_id      = aws_vpn_gateway.aws_gcp.id
  type                = "ipsec.1"
  static_routes_only  = true
}

resource "aws_vpn_connection_route" "office" {
  destination_cidr_block = "10.168.0.0/20" // fixed cidr block of gcp region on us-west-2 
  vpn_connection_id      = aws_vpn_connection.aws_gcp.id
}
```

### Apply
Gi·ªù ta ch·∫°y c√¢u l·ªánh apply n√†o üòÅ.


```
$ terraform apply
...
Plan: 32 to add, 0 to change, 0 to destroy.
```

Sau khi Terraform ch·∫°y xong, ta l√™n AWS v√† GCP ki·ªÉm tra th√¨ s·∫Ω th·∫•y c√°c resource t∆∞∆°ng ·ª©ng ƒë√£ ƒë∆∞·ª£c t·∫°o ra. V√† ƒë·ªÉ hai b√™n c√≥ th·ªÉ d·ªÖ d√†ng t∆∞∆°ng t√°c v·ªõi nhau, c√°c b·∫°n b·∫≠t Security Group cho c·∫£ hai b√™n nh√©.

## Enable Security Group
·ªû AWS Console ta b·∫•m v√†o Security Group c·ªßa VPC, ch·ªçn default SG v√† s·ª≠a Inbound rules nh∆∞ sau.

![image.png](https://images.viblo.asia/8cf40e17-2ba9-47de-bc6a-2c096bf96413.png)

Sau ƒë√≥ ta c·∫≠p nh·∫≠t Firewall Rule ·ªü b√™n GCP ƒë·ªÉ c√°c ·ª©ng d·ª•ng c·ªßa AWS c√≥ th·ªÉ tho·∫£i m√°i truy c·∫≠p t·ªõi GCP VPC. Truy c·∫≠p GCP Console, b·∫•m v√†o memu Firewall, ch·ªçn default-allow-internal v√† Edit n√≥.

![image.png](https://images.viblo.asia/a9408a45-6007-4a9c-8d61-737587afbed7.png)

Ta th√™m v√†o CIDR Block c·ªßa AWS VPC l√† 10.0.0.0/16 v√† b·∫•m save. B√¢y gi·ªù th√¨ c√°c ·ª©ng d·ª•ng c·ªßa AWS v√† GCP c·ªßa ta ƒë√£ c√≥ th·ªÉ n√≥i chuy·ªán v·ªõi nhau cho d√π c·∫£ hai ƒë·ªÅu n·∫±m trong private network üòÅ.

Github c·ªßa to√†n b·ªô series https://github.com/hoalongnatsu/terraform-series.

## K·∫øt lu·∫≠n
V·∫≠y l√† ta ƒë√£ t√¨m hi·ªÉu xong c√°ch s·ª≠ d·ª•ng Terraform v·ªõi multi-cloud. N·∫øu c√≥ th·∫Øc m·∫Øc ho·∫∑c c·∫ßn gi·∫£i th√≠ch r√µ th√™m ch·ªó n√†o th√¨ c√°c b·∫°n c√≥ th·ªÉ h·ªèi d∆∞·ªõi ph·∫ßn comment.

## M·ª•c t√¨m ki·∫øm ƒë·ªìng ƒë·ªôi

![Ho√†ng Ph√∫c](https://images.viblo.asia/9fbde6d9-5e57-4429-a903-10a961e1c96c.png)

Team c√¥ng ngh·ªá Ho√†ng Ph√∫c c·ªßa b·ªçn m√¨nh ƒë∆∞·ª£c th√†nh l·∫≠p v·ªõi nhi·ªám v·ª• l√† x√¢y d·ª±ng m·ªôt h·ªá th·ªëng c√¥ng ngh·ªá n·ªôi b·ªô cho c√¥ng ty, Ho√†ng Ph√∫c l√† m·ªôt c√¥ng ty b√°n l·∫ª trong lƒ©nh v·ª±c th·ªùi trang v√† c√≥ h∆°n 30 nƒÉm tu·ªïi ƒë·ªùi, v·ªõi chu·ªói c·ª≠a h√†ng r·∫•t nhi·ªÅu tr√™n to√†n qu·ªëc, n√™n vi·ªác v·∫≠n h√†nh c·ªßa Ho√†ng Ph√∫c l√† r·∫•t l·ªõn v√† vi·ªác x√¢y d·ª±ng ƒë∆∞·ª£c m·ªôt h·ªá th·ªëng c√¥ng ngh·ªá ƒë·ªÉ ƒë√°p ·ª©ng vi·ªác v·∫≠n h√†nh n·ªôi b·ªô cho c√¥ng ty l√† m·ªôt c√¥ng vi·ªác r·∫•t th·ª≠ th√°ch, ƒë√¢y l√† m·ªôt qu√° tr√¨nh chuy·ªÉn ƒë·ªïi s·ªë v√† team b·ªçn m√¨nh ƒë√£ l√†m ƒë∆∞·ª£c nh·ªØng b∆∞·ªõc ban ƒë·∫ßu.

Th·ª© m√† team m√¨nh th·∫•y c·∫•n duy nh·∫•t l√† c√°i website, ƒë√¢y l√† trang web m√† tr∆∞·ªõc khi team m√¨nh ƒë∆∞·ª£c th√†nh l·∫≠p ƒë√£ c√≥ m·ªôt ƒë·ªôi outsource kh√°c l√†m, v√† nh·ªØng g√¨ h·ªç ƒë·ªÉ l·∫°i cho b·ªçn m√¨nh l√† m·ªôt trang web v·ªõi ƒë·ªëng b√πi nh√πi, v·ªõi s·ªë ƒëi·ªÉm t·ª´ google l√† 1 tr√™n 100. V·∫≠y b·ªçn m√¨nh s·∫Ω l√†m g√¨ v·ªõi trang web n√†y ƒë√¢y, n·∫£n ch√≠ sao? ƒêi·ªÅu ƒë√≥ kh√¥ng c√≥ trong t·ª´ ƒëi·ªÉn c·ªßa hai s·∫øp m√¨nh, v√† v·ªõi s·ª± d·∫´n d·∫Øt c·ªßa hai s·∫øp team m√¨nh s·∫Ω bi·∫øn ƒë·ªëng website b√πi nh√πi ƒë√≥ th√†nh kim c∆∞∆°ng, nh∆∞ c√°ch b·ªçn m√¨nh ƒë√£ c·∫£i thi·ªán h·ªá th·ªëng n·ªôi b·ªô. B·ªçn m√¨nh ƒëang c·∫£i thi·ªán trang web h·∫±ng ng√†y v√† h·∫±ng ng√†y, t·ª´ 1 ƒëi·ªÉm b·ªçn m√¨nh ƒë√£ c·∫£i thi·ªán n√≥ l√™n 70 ƒëi·ªÉm, v√† m·ª•c ti√™u l√† tr√™n 90 ƒëi·ªÉm.

Hi·ªán t·∫°i team b·ªçn m√¨nh ƒëang c·∫ßn c√°c ƒë·ªìng ƒë·ªôi tham gia ƒë·ªÉ c·∫£i thi·ªán l·∫°i trang web v·ªõi s·ªë l∆∞·ª£ng ng∆∞·ªùi d√πng truy c·∫≠p kh√° l·ªõn, ƒë√¢y l√† m·ªôt th·ª≠ th√°ch r·∫•t th√∫ v·ªã, c√≥ bao gi·ªù c√°c b·∫°n ƒë∆∞·ª£c tham gia thi·∫øt k·∫ø m·ªôt h·ªá th·ªëng l·ªõn t·ª´ ƒë·∫ßu ch∆∞a, m√¨nh kh√° ch·∫Øc l√† s·ªë l∆∞·ª£ng ƒë√≥ r·∫•t √≠t. B·ªçn m√¨nh ƒë√£ c√≥ kh√°ch h√†ng, nh·ªØng g√¨ c√≤n l·∫°i l√† c·∫ßn nh·ªØng ƒë·ªìng ƒë·ªôi ƒë·ªÉ c√πng nhau ph√°t tri·ªÉn m·ªôt h·ªá th·ªëng ƒë·ªÉ ph·ª•c v·ª• r·∫•t nhi·ªÅu ng∆∞·ªùi d√πng. M·ª•c ti√™u c·ªßa c√¥ng ty Ho√†ng Ph√∫c l√† tr·ªü th√†nh nh√† b√°n l·∫ª v·ªÅ th·ªùi trang l·ªõn nh·∫•t Vi·ªát Nam, h√£y tham gia v·ªõi b·ªçn m√¨nh nh√©. M·ªôt th√†nh vi√™n trong team m√¨nh kh√¥ng y√™u c·∫ßn ph·∫£i gi·ªèi, ch·ªâ c·∫ßn h√≤a ƒë·ªìng, h·ª£p t√°c v√† s·∫µn s√†ng h·ª£p t√°c v·ªõi nhau. C√≥ th·ªÉ b·∫°n kh√¥ng l√† gi·ªèi nh·∫•t nh∆∞ng n·∫øu gia nh·∫≠p v·ªõi b·ªçn m√¨nh th√¨ b·∫°n s·∫Ω t·∫°o ra ƒë∆∞·ª£c nh·ªØng th·ª© gi√° tr·ªã nh·∫•t.

ƒê·ªìng ƒë·ªôi [Backend Engineer (Magento - PHP)](https://tuyendung.hoang-phuc.com/job/backend-engineer-magento-php-1538).

ƒê·ªìng ƒë·ªôi [Senior Backend Engineer](https://tuyendung.hoang-phuc.com/job/senior-backend-engineer-1022).

ƒê·ªìng ƒë·ªôi [Senior Frontend Engineer](https://tuyendung.hoang-phuc.com/job/senior-frontend-engineer-1021).